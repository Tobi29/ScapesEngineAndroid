/*
 * Copyright 2012-2018 Tobi29
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    def kotlinVersion = "1.2.71"
    def dokkaVersion = "0.9.17"
    def androidBuildVersion = "3.2.1"
    def androidMavenPublishVersion = "3.6.2"

    repositories {
        jcenter()
        google()
        maven {
            url "https://dl.bintray.com/kotlin/kotlin-dev"
        }
        maven {
            url "https://jitpack.io"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaVersion"
        classpath "com.android.tools.build:gradle:$androidBuildVersion"
        classpath "digital.wup:android-maven-publish:$androidMavenPublishVersion"
    }
}

allprojects {
    group = "com.github.Tobi29.ScapesEngineAndroid"

    repositories {
        jcenter()
        google()
        maven {
            url "https://jitpack.io"
        }
    }

    afterEvaluate {
        def kotlin = extensions.findByName("kotlin")
        if (kotlin != null) {
            kotlin.experimental {
                coroutines "enable"
            }
        }
    }
}

ext {
    kotlinVersion = "1.2.71"
    kotlinBase = { module -> "org.jetbrains.kotlin:$module:$kotlinVersion" }
    kotlin = { module -> kotlinBase("$module-common") }
    kotlinJVM = { module -> kotlinBase("$module") }
    kotlinJS = { module -> kotlinBase("$module-js") }

    slf4jVersion = "1.7.25"
    slf4jJVM = { module -> "org.slf4j:$module:$slf4jVersion" }

    scapesEngineVersion = "eb38dd8c91"
    scapesEngineBase = { module ->
        "com.github.Tobi29.ScapesEngine:${module}:$scapesEngineVersion"
    }
    scapesEngine = { module -> scapesEngineBase("${module}-metadata") }
    scapesEngineJVM = { module -> scapesEngineBase("${module}-jvm") }
}
